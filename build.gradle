plugins {
    id "java"
    id 'org.photonvision.tools.WpilibTools' version '2.3.1-photon'
    id "edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin" version "2020.2"
    id "com.diffplug.spotless" version "8.1.0"
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = "https://maven.photonvision.org/repository/internal/" }
    }
    wpilibRepositories.addAllReleaseRepositories(it)
    wpilibRepositories.addAllDevelopmentRepositories(it)
}

// Configure the version number.
apply from: "versioningHelper.gradle"

ext {
    pubVersion = versionString
    isDev = pubVersion.startsWith("dev")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation wpilibTools.deps.wpilibOpenCvJava("frc2025", "4.10.0-3")

    // Junit
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")

    implementation wpilibTools.deps.wpilibJava("wpimath")
    implementation wpilibTools.deps.wpilibJava("wpiunits")
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.15.2"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

def nativeName = wpilibTools.platformMapper.platformPath
ext.outputsFolder = file("$buildDir/outputs")

println("Building for $nativeName")

tasks.register('copyNativeLibrary', Sync) {
    from "${projectDir}/cmake_build/bin/"
    from "${projectDir}/cmake_build/lib/"
    // Hardcode to shared because we're not building static ever
    into "${outputsFolder}/${nativeName}/shared/"
    include "**/*.dll", "**/*.so", "**/*.dylib"
    includeEmptyDirs = false

    build.dependsOn it
    publish.dependsOn it
}

apply from: "publish.gradle"
